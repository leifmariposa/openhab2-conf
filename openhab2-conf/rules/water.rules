
/*
 * One year of consumption of water for Mostigen 1, (Leif, Kelly, Alexander and Jonathan):
 * 5449 SKr for 166 000 litres: 0,03282 Skr / litre
 */

 // Water
rule "Water"
when
  Item water changed
then
  var String flow  = transform("JSONPATH", "$.flow", (water.state).toString)
  water_flow.postUpdate(flow)

  var String last_ten_minutes  = transform("JSONPATH", "$.last_ten_minutes", (water.state).toString)
  water_last_ten_minutes.postUpdate((last_ten_minutes).toString)

  var String total  = transform("JSONPATH", "$.total", (water.state).toString)
  water_total.postUpdate((total).toString)

  var String drain  = transform("JSONPATH", "$.drain", (water.state).toString)
  var Number drain_value = Float::parseFloat(drain)
  if (drain_value != 0) {
    water_last_drain.postUpdate((drain_value).toString)
  }
end

rule "Total water"
when
  Item water_total changed
then
  var date	= now.getDayOfMonth
  var month	= now.getMonthOfYear

  if (water_at_midnight.state === NULL) {
    water_at_midnight.postUpdate((water_total.state).toString)
  }
  if (water_at_new_month.state === NULL) {
    water_at_new_month.postUpdate((water_total.state).toString)
  }

  if (water_current_date.state != date) {
    water_current_date.postUpdate((date).toString)

    if (water_four_days_ago.state != NULL) {
      water_five_days_ago.postUpdate((water_four_days_ago.state as Number).toString);
    }

    if (water_three_days_ago.state != NULL) {
      water_four_days_ago.postUpdate((water_three_days_ago.state as Number).toString);
    }

    if (water_two_days_ago.state != NULL) {
      water_three_days_ago.postUpdate((water_two_days_ago.state as Number).toString);
    }

    if (water_yesterday.state != NULL) {
      water_two_days_ago.postUpdate((water_yesterday.state as Number).toString);
    }

    water_yesterday.postUpdate(((water_total.state as Number) - (water_at_midnight.state as Number)).toString)
    water_at_midnight.postUpdate((water_total.state).toString)
  }

  if (water_current_month.state != month) {
    water_current_month.postUpdate((month).toString)

    water_last_month.postUpdate(((water_total.state as Number) - (water_at_new_month.state as Number)).toString)
    water_at_new_month.postUpdate((water_total.state).toString)
  }

  water_today.postUpdate(((water_total.state as Number) - (water_at_midnight.state as Number)).toString)
  water_month.postUpdate(((water_total.state as Number) - (water_at_new_month.state as Number)).toString)

  water_cost_today.postUpdate((water_today.state as Number) * 0.03282) // 0,03282 Skr/kWh

  //logInfo('logTime', 'water_total: ' + water_total.state + ', water_today: ' + water_today.state + ', water_month: ' + water_month.state + ', water_at_midnight: ' + water_at_midnight.state + ', water_at_new_month: ' + water_at_new_month.state)
end
